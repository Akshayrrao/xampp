@echo off
set curdir=%cd%
set tmpdir=%~dp0..\tmp

set busybox=%~dp0\busybox.exe

cls

if exist %busybox% (
	rem %busybox% true >nul 2>nul && goto :busybox
    del /q /f %busybox%
)
set busybox=%tmpdir%\busybox.exe
if exist %busybox% (
	rem %busybox% true >nul 2>nul && goto :busybox
    del /q /f %busybox%
)

set baseurl=https://frippery.org/files/busybox/
set download=%tmpdir%\download.js

if %PROCESSOR_ARCHITECTURE% == AMD64 (
  set url=%baseurl%/busybox64.exe
) else (
  set url=%baseurl%/busybox_safe.exe
)
call :download
call :busybox
goto :end

:download
if not exist %tmpdir% mkdir %tmpdir%
echo.// Generated by XAMPP >%download%
echo.var xhr = new ActiveXObject('Msxml2.ServerXMLHTTP'); >>%download%
echo.xhr.setOption(2, 13056); >>%download%
echo.xhr.open('GET', '%url%', false); >>%download%
echo.xhr.setRequestHeader('User-Agent','Mozilla/5.0 (compatible; MSIE 10.0; Windows NT 6.2; Trident/6.0)'); >>%download%
echo.xhr.setRequestHeader('Referer', '%baseurl%'); >>%download%
echo.xhr.send(null); >>%download%
echo.if (xhr.readyState != 4 ^|^| xhr.status ^> 400){ >>%download%
echo.  echo('error: ready=' + xhr.readyState + ', status=' + xhr.status); >>%download%
echo.} >>%download%
echo.var stm = new ActiveXObject('ADODB.Stream'); >>%download%
echo.stm.Type = 1; >>%download%
echo.stm.Open(); >>%download%
echo.stm.Write(xhr.responseBody); >>%download%
echo.stm.SaveToFile('%busybox:\=\\%', 2); >>%download%
echo.stm.Close(); >>%download%
cscript.exe /nologo %download%
del /q /f %download%
goto :end

:busybox
set curdir=
set tmpdir= 
set url=
set baseurl=
set download=
%busybox% sh %~dp0\install.sh
goto :end
:end
